plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id "com.google.protobuf" version "0.9.4"
    id 'me.qoomon.git-versioning' version '6.4.4'
}

group = 'dev.aisandbox'

def protobufVersion = '4.28.2'
def protocVersion = protobufVersion

gitVersioning.apply {
    refs {
        considerTagsOnBranches = true
        tag('v(?<version>.*)') {
            version = '${ref.version}'
        }
    }
    rev {
        version = "SNAPSHOT"
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

javadoc {
    destinationDir = file("build/docs/javadoc")
}

tasks.register('copyDocs', Copy) {
    group = "documentation"
    description = "Copies documentation into the build tree."
    from 'documentation'
    into 'build/docs'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

protobuf {
    // Configure the protoc executable
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    generateProtoTasks {
        // all() returns the collection of all protoc tasks
        all().configureEach { task ->
            // Here you can configure the task
        }
    }

}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    compileOnly 'org.projectlombok:lombok'
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation 'commons-cli:commons-cli:1.9.0'
    implementation 'org.jfree:jfreechart:1.5.5'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}
